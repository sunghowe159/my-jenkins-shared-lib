// vars/customPipeline.groovy

/**
 * ä¸»æµæ°´çº¿æ¨¡æ¿å‡½æ•°
 * ç»Ÿä¸€å°è£… nodeã€dockerã€å‡­è¯ã€å¼‚å¸¸æ•è·å’Œäº§ç‰©å½’æ¡£
 * @param config Map é…ç½®é¡¹
 *        nodeLabel: JenkinsèŠ‚ç‚¹æ ‡ç­¾ï¼Œé»˜è®¤ 'build-node'
 *        dockerImage: Docker é•œåƒåï¼Œéç©ºåˆ™å¯ç”¨ docker å®¹å™¨æ‰§è¡Œ
 *        dockerArgs: docker å‚æ•°ï¼Œå¦‚ -v ç¼“å­˜æŒ‚è½½ï¼Œé»˜è®¤ç©º
 *        credentials: withCredentials éœ€è¦çš„å‡­è¯åˆ—è¡¨ï¼Œé»˜è®¤ç©ºï¼ˆä¼šè‡ªåŠ¨æ·»åŠ é»˜è®¤å‡­è¯ï¼‰
 *        archiveArtifacts: æ„å»ºäº§ç‰©å½’æ¡£è·¯å¾„ï¼Œç©ºåˆ™ä¸å½’æ¡£
 * @param pipeline Closure å…·ä½“æµæ°´çº¿é€»è¾‘ï¼Œéµå®ˆè„šæœ¬å¼Pipelineè§„èŒƒ
 */
def run(Map config = [:], Closure pipeline = {}) {
    def defaultConfig = [
        nodeLabel: 'build-node',
        dockerImage: '',
        dockerArgs: '',
        credentials: [],
        archiveArtifacts: ''
    ]

    def cicd_config = defaultConfig + config

    // é»˜è®¤å‡­è¯æ·»åŠ 
    cicd_config.credentials += [
        usernamePassword(
            credentialsId: 'default-git-cred',
            usernameVariable: 'GIT_USER',
            passwordVariable: 'GIT_PASS'
        )
    ]

    this.binding.setVariable('cicd_config', cicd_config)
    cicd_code = {
        withCredentials(cicd_config.credentials) {
            def capturedException = null
            try_pipeline = {
                try {
                    pipeline() 
                    if (!currentBuild.result) {
                        currentBuild.result = 'SUCCESS'
                    }
                } catch (e) {
                    currentBuild.result = 'FAILURE'
                    capturedException = e
                    handleError(e)
                    throw e
                } finally {
                    if (cicd_config.archiveArtifacts?.trim()) {
                        archiveArtifacts artifacts: cicd_config.archiveArtifacts, allowEmptyArchive: true
                    }

                    // æˆåŠŸ / éå¤±è´¥çŠ¶æ€ï¼Œä¹Ÿå‘é€ä¸€æ¬¡ç»“æœé€šçŸ¥
                    if (currentBuild.result != 'FAILURE') {
                        notifyResult(type: currentBuild.result, error: capturedException)
                    }
                }
            }
            if(cicd_config.dockerImage) {
                runWithDocker(cicd_config) {
                    try_pipeline()
                }
            } else {
                try_pipeline()
            }
            
        }
    }

    node(cicd_config.nodeLabel) {
        cicd_code()
    }
}


/**
 * ä½¿ç”¨ docker å®¹å™¨è¿è¡Œæµæ°´çº¿
 */
def runWithDocker(Map config, Closure pipeline) {
    docker.withRegistry('https://registry.example.com', 'credentials-id') {
        docker.image(config.dockerImage).pull() // ç¡®ä¿æ‹‰å–æœ€æ–°é•œåƒ
        docker.image(config.dockerImage).inside(config.dockerArgs ?: '') {
            pipeline()
        }
    }
}

/**
 * é»˜è®¤é”™è¯¯å¤„ç†å‡½æ•°
 * å¯æ‰©å±•ä¸ºå‘é€é€šçŸ¥ç­‰
 */
def handleError(Exception err) {
    echo "âŒ æ„å»ºå¤±è´¥ï¼š${err?.getMessage() ?: 'Unknown Error'}"

    // å¯æ‰©å±•ï¼šå†™å…¥æ—¥å¿—ç³»ç»Ÿã€ç»Ÿè®¡ç³»ç»Ÿã€æ¸…ç†é€»è¾‘ç­‰
    // sendToLogSystem(err)
    // markBuildStatusInSystem('FAILURE')

    notifyResult(
        type: 'FAILURE',
        error: err
    )
}


/**
 * é€šç”¨æ„å»ºç»“æœé€šçŸ¥å·¥å…·ï¼ˆå½“å‰ç‰ˆæœ¬ä»…æ”¯æŒé‚®ä»¶ï¼‰
 * å‚æ•°:
 *   type     : 'success' / 'failure'ï¼Œé»˜è®¤æ ¹æ® currentBuild.result è‡ªåŠ¨åˆ¤æ–­
 *   subject  : é‚®ä»¶æ ‡é¢˜ï¼ˆå¯é€‰ï¼‰
 *   body     : é‚®ä»¶å†…å®¹æ­£æ–‡ï¼ˆå¯é€‰ï¼‰
 *   to       : æ”¶ä»¶äººï¼Œé»˜è®¤å– env.NOTIFY_EMAIL_TO
 */
def notifyResult(Map args = [:]) {
    def result = (args.type ?: currentBuild.result ?: 'SUCCESS').toUpperCase()
    def errorMsg = args.error?.message ?: ''
    def resultLabel = [
        'SUCCESS'  : 'æˆåŠŸ',
        'FAILURE'  : 'å¤±è´¥',
        'ABORTED'  : 'ä¸­æ­¢',
        'UNSTABLE' : 'ä¸ç¨³å®š',
        'NOT_BUILT': 'æœªæ‰§è¡Œ'
    ][result] ?: result

    def subject = args.subject ?: "[Jenkins æ„å»º${resultLabel}] ${env.JOB_NAME} #${env.BUILD_NUMBER}"

    def body = args.body ?: """
        æ„å»ºç»“æœ: ${resultLabel}
        é¡¹ç›®: ${env.JOB_NAME}
        åˆ†æ”¯: ${env.BRANCH_NAME ?: 'N/A'}
        ç¼–å·: #${env.BUILD_NUMBER}
        æ„å»ºé“¾æ¥: ${env.BUILD_URL}
        ${errorMsg ? "\nå¼‚å¸¸ä¿¡æ¯: ${errorMsg}" : ""}
    """

    def to = args.to ?: env.NOTIFY_EMAIL_TO ?: 'ci-team@example.com'

    emailext(
        subject: subject,
        body: body,
        to: to,
        mimeType: 'text/plain'
    )

    echo "ğŸ“¨ æ„å»ºç»“æœé€šçŸ¥ [${resultLabel}] å·²å‘é€è‡³ ${to}"
}

